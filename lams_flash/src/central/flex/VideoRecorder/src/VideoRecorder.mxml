<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" width="100%" backgroundGradientAlphas="[1.0, 1.0]" backgroundGradientColors="[#FFFFFF, #FFFFFF]" xmlns:ns1="org.lamsfoundation.lams.common.ui.components.*" xmlns:controls="com.asfusion.controls.*" height="100%" applicationComplete="onApplicationComplete(event)"  preinitialize="onPreinitialize(event)" enabled="true" xmlns:local="*" xmlns:ns2="org.lamsfoundation.lams.common.ui.components.*">
	
	<mx:Script>
		<![CDATA[
			import org.lamsfoundation.lams.common.util.ArgumentsToRendererFactory;
			import org.lamsfoundation.lams.common.util.VideoDisplayUtil;
			//includes
			include "HTTPServices.as"
			
			// imports
			import org.lamsfoundation.lams.common.ui.components.VideoDisplayEvent;
			import mx.events.CloseEvent;
			import org.lamsfoundation.lams.common.util.LAMSStringUtil;
			import mx.utils.StringUtil;
			import org.lamsfoundation.lams.common.ui.components.SortEvent;
			import mx.graphics.codec.PNGEncoder;
			import mx.events.SliderEvent;
			import mx.events.DragEvent;
			import com.asfusion.controls.Rating;
			import com.asfusion.controls.ratingclasses.RatingEvent;
			import mx.collections.ArrayCollection;
			import mx.events.MetadataEvent;	
			import mx.containers.TitleWindow;
			import mx.rpc.events.FaultEvent;
			import mx.controls.Alert;
			import mx.collections.XMLListCollection;
			import mx.rpc.events.ResultEvent;
			import mx.rpc.http.HTTPService;
			import adobe.utils.CustomActions;
			import mx.events.ListEvent;
			import mx.managers.PopUpManager;
			import org.lamsfoundation.lams.common.dictionary.XMLDictionary;
			
			// connection-related
			private var nc:NetConnection;
			private var client:Object;
			private var cam:Camera = null;
			private var mic:Microphone = null;
			
			// static strings
			private static var red5App:String = "oflaDemo";
			private static var servletLocation:String = "tool/lavidr10/videoRecorderActions.do";
					
			// from flashvars
			[Bindable] private var contentEditable:Boolean;
			[Bindable] private var userId:int;
			[Bindable] private var toolSessionId:int;
			[Bindable] private var toolContentId:int;
			[Bindable] private var allowUseVoice:Boolean;
			[Bindable] private var allowUseCamera:Boolean;
			[Bindable] private var allowLearnerVideoVisibility:Boolean;
			[Bindable] private var allowLearnerVideoExport:Boolean;
			[Bindable] private var allowComments:Boolean;
			[Bindable] private var allowRatings:Boolean;
			[Bindable] private var red5ServerUrl:String;
			[Bindable] private var serverUrl:String;
			[Bindable] private var languageXML:XML;
			[Bindable] private var mode:String;

			// misc
			private var filename:String = "";
			[Bindable] private var itemClicked:Object = null;
			private var ratingClicked:Rating;
			[Bindable] private var dictionary:XMLDictionary;
			[Bindable] private var videoRecordings:XMLList = null;
			private var playTimer:Timer;
			private var recTimer:Timer;
			private var metadata:Object;
			
			// ui stuff
			private var getDetailsPopUp:GetRecordingDetailsPopUp;
			private var addCommentPopUp:AddCommentPopUp;
			private var previewImage:BitmapData;
			
			// constants
			public const TEACHER_MODE:String = "teacher";
			public const AUTHOR_MODE:String = "author";
			public const LEARNER_MODE:String = "learner";
							
			// called from preinitialize
			private function onPreinitialize(event:Event):void {
				// get flashvars
				getFlashVars();
								
				// if in teacher or author mode
				if(mode == TEACHER_MODE || mode == AUTHOR_MODE){
					allowComments = true;
					allowRatings = true;
					allowLearnerVideoExport = true;
					allowLearnerVideoVisibility = true;
					allowUseCamera = true;
					allowUseVoice = true;	
				}
								
				// for testing purposes (when running in debug)
				if(toolSessionId == 0){
					contentEditable = true;
					userId = 1;
					toolSessionId = 4;
					toolContentId = 1;
					allowUseVoice = true;
					allowUseCamera = false;
					allowComments = false;
					allowRatings = true;
					allowLearnerVideoExport = false;
					allowLearnerVideoVisibility = true;
					red5ServerUrl = "rtmp://172.20.100.22/";
					serverUrl = "http://172.20.100.220:8080/lams/";
					mode = AUTHOR_MODE;
				}
				
				/*
				Alert.show(
					"contentEditable: " + contentEditable +
					"\nuserId: " + userId +
					"\ntoolSessionId: " + toolSessionId +
					"\ntoolContentId: " + toolContentId +
					"\nallowUseVoice: " + allowUseVoice +
					"\nallowUseCamera: " + allowUseCamera +
					"\nallowLearnerVideoVisibility: " + allowLearnerVideoVisibility +
					"\nallowLearnerVideoExport: " + allowLearnerVideoExport +
					"\nallowComments: " + allowComments +
					"\nallowRatings: " + allowRatings +
					"\nred5ServerUrl: " + red5ServerUrl +
					"\nserverUrl: " + serverUrl +
					"\nmode: " + mode);
				*/
			}
			
			// called from applicationComplete
			private function onApplicationComplete(event:Event):void {
				// init labels if languageXML is not empty
				if(languageXML.toString() != ""){
					dictionary = new XMLDictionary(languageXML);
					setLabels();
					setTooltips();
				}
				else{
					dictionary = new XMLDictionary(new XML(""));
				}
				
				// add some listeners
				addListeners();
				
				// add the app name to the red5 url to complete the url
				red5ServerUrl += red5App;
				
				// add the servlet location to the server url to complete
				serverUrl += servletLocation;
				
				// if the cam is to be used, set that up
				if(allowUseCamera){
					cam = VideoDisplayUtil.setupCamera();
					videoDisplay.attachCamera(cam);
				}
				// else remove the view camera button
				else{
					recordingButtonBox.removeChild(recordingButtonBox.getChildByName("viewCameraButton"));
				}
				
				// if the mic is to be used, set that up
				if(allowUseVoice){
					mic = VideoDisplayUtil.setupMic();
					videoDisplay.attachMic(mic)
				}
				
				// if neither mic or camera are to be used
				if(!allowUseCamera && !allowUseVoice){
					// remove recording button
					removeChild(getChildByName("recordingBox"));
					
					// stretch the video list
					videoList.setStyle("bottom", 10);
					videoList.invalidateList();
				}
				else{
					if(!isWebcamInfoSet())
					{
						Security.showSettings();
						setWebcamInfo(true);
					}
				}
				
				// init components that need it
				videoInformation.init();
				
				// disable playback buttons and enable the video list
				enablePlaybackButtons(false);
				enableVideoList(true);
				
				// create the connection
				createConnection();
			}
			
			// gets flashvars from request
			private function getFlashVars():void{
				red5ServerUrl = Application.application.parameters.red5ServerUrl;
				serverUrl = Application.application.parameters.serverUrl;
				contentEditable = LAMSStringUtil.stringToBool(Application.application.parameters.contentEditable);
				userId = Application.application.parameters.userId;
				toolSessionId = Application.application.parameters.toolSessionId;
				toolContentId = Application.application.parameters.toolContentId;
				allowUseVoice = LAMSStringUtil.stringToBool(Application.application.parameters.allowUseVoice);
				allowUseCamera = LAMSStringUtil.stringToBool(Application.application.parameters.allowUseCamera);
				allowLearnerVideoVisibility = LAMSStringUtil.stringToBool(Application.application.parameters.allowLearnerVideoVisibility);
				allowLearnerVideoExport = LAMSStringUtil.stringToBool(Application.application.parameters.allowLearnerVideoExport);
				allowComments = LAMSStringUtil.stringToBool(Application.application.parameters.allowComments);
				allowRatings = LAMSStringUtil.stringToBool(Application.application.parameters.allowRatings);
				languageXML = XML(Application.application.parameters.languageXML);
				mode = Application.application.parameters.mode;
			}
			
			// sets initial labels
			private function setLabels():void{
				playButton.label = dictionary.getLabel("videorecorder.play");
				refreshButton.label = dictionary.getLabel("videorecorder.refresh");
				sortAuthorButton.label = dictionary.getLabel("videorecorder.author");
				sortByLabel.text = dictionary.getLabelAndConcatenate("videorecorder.sort.by", [":"]);
				sortDateButton.label = dictionary.getLabel("videorecorder.date");
				sortTitleButton.label = dictionary.getLabel("videorecorder.title");
				videosLabel.text = dictionary.getLabelAndConcatenate("videorecorder.videos", [":"]);
				videoInformationLabel.text = dictionary.getLabelAndConcatenate("videorecorder.video.information", [":"]);
				videoInformation.titleLabel.text = dictionary.getLabelAndConcatenate("videorecorder.title", [":"]);
				videoInformation.authorLabel.text = dictionary.getLabelAndConcatenate("videorecorder.author", [":"]);
				videoInformation.ratingLabel.text = dictionary.getLabelAndConcatenate("videorecorder.rating", [":"]);
				videoInformation.descriptionLabel.text = dictionary.getLabelAndConcatenate("videorecorder.description", [":"]);
				videoInformation.commentsLabel.text = dictionary.getLabelAndConcatenate("videorecorder.comment", ["s:"]);
				videoInformation.addCommentButton.label = dictionary.getLabel("videorecorder.add.comment");
				startRecButton.label = dictionary.getLabel("videorecorder.start.recording");
				viewCameraButton.label = dictionary.getLabel("videorecorder.view.camera");
				videoRecorderPanel.status = dictionary.getLabel("videorecorder.waiting");
			}
			
			// sets initial tooltips
			private function setTooltips():void{
				playButton.toolTip = dictionary.getLabelAndConcatenate("videorecorder.tooltip.play", [" (", dictionary.getLabel("videorecorder.disabled"), ")"]);
				refreshButton.toolTip = dictionary.getLabelAndConcatenate("videorecorder.tooltip.refresh", [" (", dictionary.getLabel("videorecorder.disabled"), ")"]);
				sortAuthorButton.toolTip = dictionary.getLabelAndConcatenate("videorecorder.tooltip.sort.author", [" (", dictionary.getLabel("videorecorder.disabled"), ")"]);
				sortDateButton.toolTip = dictionary.getLabelAndConcatenate("videorecorder.tooltip.sort.date", [" (", dictionary.getLabel("videorecorder.disabled"), ")"]);
				sortTitleButton.toolTip = dictionary.getLabelAndConcatenate("videorecorder.tooltip.sort.title", [" (", dictionary.getLabel("videorecorder.disabled"), ")"]);
				viewCameraButton.toolTip = dictionary.getLabel("videorecorder.tooltip.start.camera");
				startRecButton.toolTip = dictionary.getLabel("videorecorder.tooltip.start.recording");
				
				videoInformation.addCommentButton.toolTip = dictionary.getLabelAndConcatenate("videorecorder.tooltip.add.comment", [" (", dictionary.getLabel("videorecorder.disabled"), ")"]);
				videoInformation.ratingButton.toolTip = dictionary.getLabelAndConcatenate("videorecorder.tooltip.rate.recording", [" (", dictionary.getLabel("videorecorder.disabled"), ")"]);
			}
			
			// adds listeners
			private function addListeners():void{
				videoInformation.ratingButton.addEventListener(RatingEvent.SELECTION_CHANGE, addRatingClick);
				videoInformation.addCommentButton.addEventListener(MouseEvent.CLICK, addCommentClick);
				videoList.addEventListener(MouseEvent.CLICK, deleteClick);
				sortButtonGroup.addEventListener(SortEvent.EVENT_TYPE, sortRecordingsHandler);
				playButton.addEventListener(MouseEvent.CLICK, playClick);
				startRecButton.addEventListener(MouseEvent.CLICK, startRecordingClick);
				viewCameraButton.addEventListener(MouseEvent.CLICK, viewCameraClick);
				
				videoDisplay.addEventListener(VideoDisplayEvent.METADATA, onMetaData);
				videoDisplay.addEventListener(VideoDisplayEvent.PAUSE, onPause);
				videoDisplay.addEventListener(VideoDisplayEvent.UNPAUSE, onUnpause);
				videoDisplay.addEventListener(VideoDisplayEvent.READY, onReady);
				videoDisplay.addEventListener(VideoDisplayEvent.RESET, onReset);
				videoDisplay.addEventListener(VideoDisplayEvent.STARTCAM, onStartCamera);
				videoDisplay.addEventListener(VideoDisplayEvent.STOPCAM, onStopCamera);
				videoDisplay.addEventListener(VideoDisplayEvent.STARTPUBLISH, onStartPublish);
				videoDisplay.addEventListener(VideoDisplayEvent.STOPPUBLISH, onStopPublish);
				videoDisplay.addEventListener(VideoDisplayEvent.COMPLETE, onComplete);
			}
			
			// checks for previous webcam info
			private function isWebcamInfoSet():Boolean{
				var lamsVideoRecorderInfo:Object = SharedObject.getLocal("lamsWebcamInfo");
				
				if(lamsVideoRecorderInfo.data.webcamSet){
					return lamsVideoRecorderInfo.data.webcamSet;
				}else{
					return false;
				}
					
			}
			
			// sets webcam info
			private function setWebcamInfo(checked:Boolean):void{
				var lamsVideoRecorderInfo:Object = SharedObject.getLocal("lamsWebcamInfo");
				lamsVideoRecorderInfo.data.webcamSet = checked;
			}
			
			// creates connection to red5 server
			private function createConnection():void{
				// create basic netConnection object
				nc = new NetConnection();
			
				// add some listeners to it
				nc.addEventListener(NetStatusEvent.NET_STATUS, ncStatusHandler);
				nc.addEventListener(SecurityErrorEvent.SECURITY_ERROR, securityErrorHandler);
				nc.addEventListener(AsyncErrorEvent.ASYNC_ERROR, asyncErrorHandler);
				
				// set the nc to the videodisplay
				videoDisplay.setNetConnection(nc);
				
				// disable playback buttons
				enablePlaybackButtons(false);
				
				// connect to the local Red5 server and wait for callback
				nc.connect(red5ServerUrl);
				
				// wait for NetStatusEvent to be thrown
			}
			
			// callback called on NetConnection connect event
			private function ncStatusHandler(event:NetStatusEvent):void {
				// get the info object
				var infoObject:Object = event.info;
				
				// print its properties
				VideoDisplayUtil.printInfoObject("ncStatusHandler", infoObject);
  
				// if successful connection
				if (infoObject.code == "NetConnection.Connect.Success") {
					// enable application
					Application.application.enabled = true;
					
					// call server and get recordings, update datagrid with results
					getRecordingsFromServer(sortButtonGroup.sortBy, sortButtonGroup.sortDirection);	
				}
				// if connection unsuccessful
				else if(infoObject.code == "NetConnection.Connect.InvalidApp"){
					Application.application.enabled = false;			
    				// pop an alert
					Alert.show(dictionary.getLabel("videorecorder.web.application.not.available"));
				}
				else if(infoObject.code == "NetConnection.Connect.Failed"){
					Application.application.enabled = false;				
    				// pop an alert
					Alert.show(dictionary.getLabel("videorecorder.net.connection.not.connected"));
				}
				else if(infoObject.code == "NetConnection.Connect.Closed"){
					Application.application.enabled = false;
    				// pop an alert
					Alert.show(dictionary.getLabel("videorecorder.net.connection.closed"));
				}
			}
			
			// netconnection security error handler
			private function securityErrorHandler(event:SecurityErrorEvent):void {
				trace("securityErrorHandler: " + event);
			}
			
			// netconnection async error handler
			private  function asyncErrorHandler(event:AsyncErrorEvent):void {
				trace("securityErrorHandler: " + event);
			} 
        	
        	// ugly onBW callback
			NetConnection.prototype.onBWDone = function(infoObject) {
				// print its properties
    			Application.application.printInfoObject("onBWDone", infoObject)
			}
			
			// handler to sort and update recordings
        	private function sortRecordingsHandler(event:SortEvent):void{
        		getRecordingsFromServer(sortButtonGroup.sortBy, sortButtonGroup.sortDirection);	
        	}
        	
        	// handler called when the confirm action ("ok", "cancel", "save") on a popup is hit
        	private function popUpRemovedHandler(event:Event):void{
        		// if the given popup is of type getDetailsPopUp
        		if(event.target == getDetailsPopUp){
        			// get the details
        			var title:String = getDetailsPopUp.titleInput.text;
        			var description:String = getDetailsPopUp.descriptionInput.text;
        			var recordingId:int = -1;
        			var rating:Number = 0;
        			
        			// call the http service to save a recording
        			saveRecordingToServer(userId, title, description, filename + ".flv", rating, toolSessionId, recordingId);
        		}
				// if the given popup is of type addCommentPopUp
        		else if(event.target == addCommentPopUp){
        			// get the details
        			var comment:String = addCommentPopUp.commentInput.text;
        			var commentId:int = -1;
        			var toolSessionId:int = toolSessionId;
        			var recordingId:int = itemClicked.recordingId;
        			
        			// call the http service to save a comment
        			saveCommentToServer(toolSessionId, recordingId, userId, commentId, comment);
        		}
        		
        		// no matter what popup, close it
        		if(event.target is TitleWindow){
        			PopUpManager.removePopUp(TitleWindow(event.target));
        		}
        	}
			
			// view camera click handler
			private function viewCameraClick(event:MouseEvent):void{
				if(!videoDisplay.isCameraViewed){
					// reset the video display
					videoDisplay.reset();
					
					// start the camera
					videoDisplay.startViewCamera();
				}
				else if(videoDisplay.isCameraViewed){
					videoDisplay.stopViewCamera();
				}
			}
			
			// play click handler
			private function playClick(event:MouseEvent):void{
				videoDisplay.play();
			}
			
			// recording click handler
			private function startRecordingClick(event:MouseEvent):void{
				if(!videoDisplay.isPublishing){
					filename = VideoDisplayUtil.createFilename(toolSessionId, userId);
					videoDisplay.startPublish(filename);
				}
				else if(videoDisplay.isPublishing)
					videoDisplay.stopPublish();
			}
			
			// add comment click handler
			private function addCommentClick(event:MouseEvent):void{
				addCommentPopUp = AddCommentPopUp(PopUpManager.createPopUp(this, AddCommentPopUp, true));
				PopUpManager.centerPopUp(addCommentPopUp);
				addCommentPopUp.init(dictionary);
				addCommentPopUp.addEventListener("popUpClose", popUpRemovedHandler);
			}
			
			// add rating click handler
			private function addRatingClick(event:RatingEvent):void{
				if(!Rating(event.currentTarget).voted){
					var recordingId:int = itemClicked.recordingId;
					var ratingId:int = -1;
					var rating:Number = event.selectedValue;
					ratingClicked = Rating(event.currentTarget);
					
					saveRatingToServer(toolSessionId, ratingId, userId, rating, recordingId);			
				}
			}
			
			// delete recording click handler
			private function deleteClick(event:MouseEvent):void{
				if(event.target.hasOwnProperty("id") && event.target.id == "deleteButton"){
					Alert.show(dictionary.getLabel("videorecorder.message.sure.delete"), dictionary.getLabel("videorecorder.confirm"), 3, this, confirmDeleteClick);  
				}
			}
			
			// confirm delete click handler
			private function confirmDeleteClick(event:CloseEvent):void{
				if (event.detail==Alert.YES){
					var recordingId:int = itemClicked.recordingId;
                	deleteRecordingFromServer(recordingId);
                	videoInformation.resetInformation();
                	videoDisplay.reset();
                }
			}
			
			// click handler for the videoDataGrid list
			private function videoListClick(event:ListEvent):void {
				if(videoList.enabled){	
					// reset video information
					videoInformation.resetInformation();
					
					// store last item clicked
					itemClicked = event.itemRenderer.data;
					
					// enable video information buttons
					videoInformation.enableVideoInformationButtons(true);
				
					// scroll the video information box back up to top
					videoInformation.verticalScrollPosition = 0;
					
					// update fields
					videoInformation.title.text = itemClicked.title;
					videoInformation.description.text = itemClicked.description;
					videoInformation.author.text = itemClicked.author;
					videoInformation.ratingButton.value = itemClicked.rating;
					videoInformation.printComments(itemClicked.comments);
					
					videoInformation.addCommentButton.toolTip = dictionary.getLabel("videorecorder.tooltip.add.comment");
					
					// show/hide ratings and comments depending on toolContentId status
					if(LAMSStringUtil.stringToBool(itemClicked.isToolContent)){
						videoInformation.ratingsBox.visible = false;
						videoInformation.commentsBox.visible = false;
					}
					else{
						videoInformation.ratingsBox.visible = true;
						videoInformation.commentsBox.visible = true;
					}
					
					// if no vote has been done, enable voting and show average
					if(itemClicked.userRating == -1){
						videoInformation.ratingButton.voted = false;
						videoInformation.ratingButton.toolTip = dictionary.getLabel("videorecorder.tooltip.rate.recording");
					}
					// if vote has been done, show user's vote and disable voting
					else{
						videoInformation.ratingButton.voted = true;
						videoInformation.ratingButton.votedValue = itemClicked.userRating;
						videoInformation.ratingButton.toolTip = dictionary.getLabelAndConcatenate("videorecorder.tooltip.already.rated", [" ", String(itemClicked.userRating)]);
					}
					
					// make ready item clickedo
					makeReadytItemClicked();
				}
				else{
					itemClicked = null;
					videoList.selectedItem = null;
					videoInformation.resetInformation();
				}
			}
			
			// makes the clicked item ready with the video display
			private function makeReadytItemClicked():void{
				// if playing a sound file
				if(LAMSStringUtil.stringToBool(itemClicked.isJustSound)){
					// change the view stack to audio view
					videoDisplay.makeReadyAudio(itemClicked.filename);
				}else{
					// change the view stack to player view
					videoDisplay.makeReadyVideo(itemClicked.filename);
				}
			}
			
			// onMetaData callback, called from videoDisplay
			private function onMetaData(event:VideoDisplayEvent):void{
				videoRecorderPanel.status = dictionary.getLabel("videorecorder.buffering");
				
				seekSlider.minimum = 0;
				seekSlider.maximum = event.metadata.duration;
				
				playTimer = new Timer(10);
				playTimer.addEventListener(TimerEvent.TIMER, updateSeekSlider);
				playTimer.start();
			}
			
			// onReady callback, called from videoDisplay
			private function onReady(event:VideoDisplayEvent):void{
				enablePlaybackButtons(true);
				videoRecorderPanel.status = dictionary.getLabel("videorecorder.ready");
			}
			
			// onReset callback, called from videoDisplay
			public function onReset(event:VideoDisplayEvent):void{
				// reset play buttons
				enablePlaybackButtons(false);
				
				// set panel status label
				videoRecorderPanel.status = dictionary.getLabel("videorecorder.waiting");
			}
			
			// onUnpause callback, called from videoDisplay
			private function onUnpause(event:VideoDisplayEvent):void{
				// set panel status label
				videoRecorderPanel.status = dictionary.getLabel("videorecorder.playing");
				
				playButton.label = dictionary.getLabel("videorecorder.pause");
				playButton.toolTip = dictionary.getLabel("videorecorder.tooltip.pause");
			}
			
			// onPause callback, called from videoDisplay
			private function onPause(event:VideoDisplayEvent):void{
				// set panel status label
				videoRecorderPanel.status = dictionary.getLabel("videorecorder.paused");
				
				playButton.label = dictionary.getLabel("videorecorder.resume");
				playButton.toolTip = dictionary.getLabel("videorecorder.tooltip.resume");
			}
			
			// onStartPublish callback, called from videoDisplay
			private function onStartPublish(event:VideoDisplayEvent):void{
				// set panel status label
				videoRecorderPanel.status = dictionary.getLabel("videorecorder.recording");
				
				// change the start recording button's label
				startRecButton.label = dictionary.getLabel("videorecorder.stop.recording");
				startRecButton.toolTip = dictionary.getLabel("videorecorder.tooltip.stop.recording");
				
				viewCameraButton.enabled = false;
				viewCameraButton.label = dictionary.getLabel("videorecorder.view.camera");
				viewCameraButton.toolTip = dictionary.getLabelAndConcatenate("videorecorder.tooltip.start.camera", [" (", dictionary.getLabel("videorecorder.disabled"), ")"]);
				
				recTimer = new Timer(250);
				recTimer.addEventListener(TimerEvent.TIMER, updateTimerLabelForRecording);
				recTimer.start();
			}
			
			// onStopPublish callback, called from videoDisplay
			private function onStopPublish(event:VideoDisplayEvent):void{		
				// change the start recording button's label
				startRecButton.label = dictionary.getLabel("videorecorder.start.recording");
				startRecButton.toolTip = dictionary.getLabel("videorecorder.tooltip.start.recording");
				
				// set panel status label
				videoRecorderPanel.status = dictionary.getLabel("videorecorder.waiting");
				
				viewCameraButton.enabled = true;
				viewCameraButton.toolTip = dictionary.getLabel("videorecorder.tooltip.start.camera");
				
				getDetailsPopUp = GetRecordingDetailsPopUp(PopUpManager.createPopUp(this, GetRecordingDetailsPopUp, true));
				PopUpManager.centerPopUp(getDetailsPopUp);
				getDetailsPopUp.init(dictionary);
				getDetailsPopUp.addEventListener("popUpClose", popUpRemovedHandler);
				
				recTimer.stop();
			}
			
			// onStartCamera callback, called from videoDisplay
			private function onStartCamera(event:VideoDisplayEvent):void{				
				// reset video information
				videoInformation.resetInformation();
				
				// change the view camera button's label
				viewCameraButton.label = dictionary.getLabel("videorecorder.stop.camera");
				viewCameraButton.toolTip = dictionary.getLabel("videorecorder.tooltip.stop.camera");
				
				// set panel status label
				videoRecorderPanel.status = dictionary.getLabel("videorecorder.waiting");
				
				// disable buttons
				enablePlaybackButtonsAndVideoList(false);
				videoInformation.enableVideoInformationButtons(false);
			}
			
			// onStopCamera callback, called from videoDisplay
			private function onStopCamera(event:VideoDisplayEvent):void{
				// change the view camera button's label
				viewCameraButton.label = dictionary.getLabel("videorecorder.view.camera");
				viewCameraButton.toolTip = dictionary.getLabel("videorecorder.tooltip.start.camera");
				
				// set panel status label
				videoRecorderPanel.status = dictionary.getLabel("videorecorder.waiting");
				
				// clear item clicked
				videoList.selectedItem = null;
				itemClicked = null;
				
				// enable buttons
				enablePlaybackButtonsAndVideoList(true);
			}
			
			// onComplete callback, called from videoDisplay
			private function onComplete(event:VideoDisplayEvent):void{
				// set panel status label
				videoRecorderPanel.status = dictionary.getLabel("videorecorder.ready");
				
				// reset play button labels
				playButton.label = dictionary.getLabel("videorecorder.play");
				playButton.toolTip = dictionary.getLabel("videorecorder.tooltip.play");
			}
			
			// seeks to the given location of recording
			private function seekSliderChange(event:SliderEvent):void {
				videoDisplay.seek(event.value);
			}
			
			// seek slider click handler
			private function seekSliderClick(event:SliderEvent):void {
				// if seekslider is enabled
				if(seekSlider.enabled){
					// if the mouse is pressed
					if(event.type == SliderEvent.THUMB_PRESS){
						// stop the timer so that the bar seekSlider can manually be controlled
						playTimer.stop();
					}
					// if the mouse is released
					else if(event.type == SliderEvent.THUMB_RELEASE){
						// start the timer again and allow it to regain control of the seekSlider
						playTimer.start();
					}
				}
			}
						
			// update method called from a timer (launched when metadata is received)
			private function updateSeekSlider(event:TimerEvent):void{
				// if the video isn't actually playing yet
				if(videoDisplay.playMode == videoDisplay.INIT_MODE || videoDisplay. playMode == videoDisplay.READY_MODE){
					seekSlider.value = 0;
					timerLabel.text = "0:00" + " / " + VideoDisplayUtil.secondsToString(String(videoDisplay.duration));
				}
				// otherwise, if it is playing
				else if(videoDisplay.playMode == videoDisplay.PLAY_MODE || videoDisplay.playMode == videoDisplay.PAUSE_MODE){
					seekSlider.value = videoDisplay.time;
					timerLabel.text = VideoDisplayUtil.secondsToString(String(videoDisplay.time)) + " / " + VideoDisplayUtil.secondsToString(String(videoDisplay.duration));					
				}
			}
			
			// updates timerLabel when recording
			private function updateTimerLabelForRecording(event:TimerEvent):void{
				var timer:Timer = Timer(event.currentTarget);
				timerLabel.text = VideoDisplayUtil.secondsToString(String(timer.currentCount * timer.delay / 1000));
			}
			   	       	
	       	// enables/disables the playback related buttons and video list
			private function enablePlaybackButtonsAndVideoList(enabled:Boolean):void{
				enablePlaybackButtons(enabled);
				enableVideoList(enabled);
			}	    
	        
	        // enables/disables the playback related buttons
	        private function enablePlaybackButtons(enabled:Boolean):void{
	        	var trueEnable:Boolean = enabled && itemClicked != null;
	        	
	        	// play button stuff
	        	playButton.label = dictionary.getLabel("videorecorder.play");
	        	
				playButton.enabled = trueEnable;
				
				if(trueEnable)
					playButton.toolTip = dictionary.getLabel("videorecorder.tooltip.play");
				else
					playButton.toolTip = dictionary.getLabelAndConcatenate("videorecorder.tooltip.play", [" (", dictionary.getLabel("videorecorder.disabled"), ")"]);
				
				// seekslider stuff
				seekSlider.enabled = trueEnable;
				
				if(!trueEnable){
					// if a timer exists, clear it
					if(playTimer){
						playTimer.stop();
						playTimer = null;
					}
					
					seekSlider.minimum = 0;
					seekSlider.maximum = 0;
					seekSlider.value = seekSlider.minimum;
					
					timerLabel.text = "0:00 / 0:00";
				}
	        }
	        
	        // enables/disables the video list
	        private function enableVideoList(enabled:Boolean):void{
	  			refreshButton.enabled = enabled;
				videoList.enabled = enabled;
				sortDateButton.enabled = enabled;
				sortAuthorButton.enabled = enabled;
				sortTitleButton.enabled = enabled;
				
				if(enabled){
					refreshButton.toolTip = dictionary.getLabel("videorecorder.tooltip.refresh");
					sortAuthorButton.toolTip = dictionary.getLabel("videorecorder.tooltip.sort.author");
					sortDateButton.toolTip = dictionary.getLabel("videorecorder.tooltip.sort.date");
					sortTitleButton.toolTip = dictionary.getLabel("videorecorder.tooltip.sort.title");
				}
				else{
					refreshButton.toolTip = dictionary.getLabelAndConcatenate("videorecorder.tooltip.refresh", [" (", dictionary.getLabel("videorecorder.disabled"), ")"]);
					sortAuthorButton.toolTip = dictionary.getLabelAndConcatenate("videorecorder.tooltip.sort.author", [" (", dictionary.getLabel("videorecorder.disabled"), ")"]);
					sortDateButton.toolTip = dictionary.getLabelAndConcatenate("videorecorder.tooltip.sort.date", [" (", dictionary.getLabel("videorecorder.disabled"), ")"]);
					sortTitleButton.toolTip = dictionary.getLabelAndConcatenate("videorecorder.tooltip.sort.title", [" (", dictionary.getLabel("videorecorder.disabled"), ")"]);
				}
				
				// if videoRecordings is defined
				if(videoRecordings){
					var length:int = videoRecordings.length();
					// get children from videoList and enable/disable
					for(var i:int = 0; i < length; i++){
						
						var videoProfile:VideoProfile = VideoProfile(videoList.indexToItemRenderer(i));
						var deleteButton:Button = videoProfile.deleteButton;
						var exportButton:Button = videoProfile.exportButton;
						
						if(deleteButton){
							deleteButton.enabled = enabled;
							
							if(enabled)
								deleteButton.toolTip = dictionary.getLabel("videorecorder.tooltip.delete.recording");
							else
								deleteButton.toolTip = dictionary.getLabelAndConcatenate("videorecorder.tooltip.delete.recording", [" (", dictionary.getLabel("videorecorder.disabled"), ")"]);
						}
							
						
						if(exportButton){
							exportButton.enabled = enabled;
							
							if(enabled)
								exportButton.toolTip = dictionary.getLabel("videorecorder.tooltip.export.recording");
							else
								exportButton.toolTip = dictionary.getLabelAndConcatenate("videorecorder.tooltip.export.recording", [" (", dictionary.getLabel("videorecorder.disabled"), ")"]);
						}
							
					}
				}    	
	        }
	        
	        // getters
	        public function getUserId():int{
	        	return userId;
	        }
	        
	        public function getAllowLearnerVideoExport():Boolean{
	        	return allowLearnerVideoExport;
	        }
	        
	       	public function getAllowLearnerVideoVisibility():Boolean{
	        	return allowLearnerVideoVisibility;
	        }
	        
	        public function getAllowRatings():Boolean{
	        	return allowRatings;
	        }
	        
	        public function getAllowComments():Boolean{
	        	return allowComments;
	        }
	        
	        public function getDictionary():XMLDictionary{
	        	return dictionary;
	        }
	        
	        public function getMode():String{
	        	return mode;
	        }
	    ]]>
	</mx:Script>

	<mx:Panel width="340" height="310" layout="absolute" left="10" top="10" title="Display:" id="videoRecorderPanel" borderColor="#E2E2E2">
	<ns1:VideoDisplay id="videoDisplay" width="320" height="240"/>
	<mx:ControlBar height="38" y="242">
		<mx:Button id="playButton" label="Play" width="75"/>
		<mx:HSlider width="100%" id="seekSlider" change="seekSliderChange(event)" dataTipFormatFunction="VideoDisplayUtil.secondsToString" thumbPress="seekSliderClick(event)" thumbRelease="seekSliderClick(event)"/>
		<mx:Label text="0:00 / 0:00" width="70" textAlign="center" id="timerLabel"/>
	</mx:ControlBar>
	</mx:Panel>
	<mx:Label id="videoInformationLabel" x="10" y="328" text="Video information:" fontWeight="bold"/>
	<local:VideoInformation id="videoInformation" allowComments="{allowComments}" allowRatings="{allowRatings}" dictionary="{dictionary}"/>
	<mx:HBox left="358" top="10" right="10">
		<mx:Label id="videosLabel" text="Videos:" fontWeight="bold"/>
	</mx:HBox>
	<mx:TileList borderStyle="none" id="videoList" itemRenderer="{new ArgumentsToRendererFactory({userId:this.userId, allowLearnerVideoExport:this.allowLearnerVideoExport, allowLearnerVideoVisibility:this.allowLearnerVideoVisibility, mode:this.mode, dictionary:this.dictionary},VideoProfile)}" dataProvider="{videoRecordings}" paddingLeft="0" paddingRight="0" paddingTop="0" paddingBottom="0" itemClick="videoListClick(event)" top="62" left="358" bottom="70" right="10"></mx:TileList>
	<mx:HBox verticalAlign="middle" left="358" top="36" right="10">
		<mx:Label id="sortByLabel" text="Sort by:" fontWeight="normal" fontStyle="normal"/>
		<ns2:SortButtonGroup width="100%" horizontalGap="3" id="sortButtonGroup">
			<ns2:SortButton id="sortDateButton" label="Date"  sortBy="date" selected="true" horizontalGap="0" sortDirection="descending"/>
			<ns2:SortButton id="sortAuthorButton" label="Author"  sortBy="author" horizontalGap="0"/>
			<ns2:SortButton id="sortTitleButton" label="Title"  sortBy="title" horizontalGap="0"/>
		</ns2:SortButtonGroup>
		<mx:Button label="Update" id="refreshButton" click="getRecordingsFromServer(sortButtonGroup.sortBy, sortButtonGroup.sortDirection)" height="17" textAlign="center"/>
	</mx:HBox>
	<mx:VBox height="52" horizontalAlign="right" right="10" bottom="10" left="357" id="recordingBox">
		<mx:Label id="recordingControlsLabel" text="Recording controls:" fontWeight="bold"/>
		<mx:HBox width="100%" height="100%" horizontalAlign="right"  id="recordingButtonBox">
			<mx:Button id="viewCameraButton" label="View Camera"/>
			<mx:Button fontWeight="bold" id="startRecButton" label="Start Recording"/>
		</mx:HBox>
	</mx:VBox>
</mx:Application>
