<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" creationComplete="initApp();" width="793" height="454" backgroundGradientAlphas="[1.0, 1.0]" backgroundGradientColors="[#FFFFFF, #FFFFFF]">
       
    <!-- httpservices -->
    <mx:HTTPService 
        id="getBranchesService"
        url="http://172.20.100.220:8080/lams/monitoring/branching.do"
        method="POST"
        resultFormat="xml"
        result="getBranchesSuccessHandler(event);"
        fault="getBranchesFaultHandler(event);">
        
        <mx:request xmlns="">
            <method>getBranches</method>
            <activityID></activityID>
        </mx:request>
    </mx:HTTPService>

    <mx:HTTPService 
        id="getMembersNotBranchedService"
        url="http://172.20.100.220:8080/lams/monitoring/branching.do"
        method="POST"
        resultFormat="xml"
        result="getMembersNotBranchedSuccessHandler(event);"
        fault="getMembersNotBranchedFaultHandler(event);">
        
        <mx:request xmlns="">
            <method>getClassMembersNotGrouped</method>
            <activityID></activityID>
        </mx:request>
    </mx:HTTPService>

    <mx:HTTPService 
        id="getBranchMembersService"
        url="http://172.20.100.220:8080/lams/monitoring/branching.do"
        method="POST"
        resultFormat="xml"
        result="getBranchMembersSuccessHandler(event);"
        fault="getBranchMembersFaultHandler(event);">
        
        <mx:request xmlns="">
            <method>getBranchMembers</method>
            <branchID></branchID>
            <activityID></activityID>
        </mx:request>
    </mx:HTTPService>

    <mx:HTTPService 
        id="addMembersService"
        url="http://172.20.100.220:8080/lams/monitoring/grouping.do"
        method="POST"
        resultFormat="xml"
        result="addMembersSuccessHandler(event);"
        fault="addMembersFaultHandler(event);">
        
        <mx:request xmlns="">
            <method>addMembers</method>
            <activityID></activityID>
            <branchID></branchID>
            <members></members>
        </mx:request>
    </mx:HTTPService>
    
    <mx:HTTPService 
        id="removeMembersService"
        url="http://172.20.100.220:8080/lams/monitoring/grouping.do"
        method="POST"
        resultFormat="xml"
        result="removeMembersSuccessHandler(event);"
        fault="removeMembersFaultHandler(event);">
        
        <mx:request xmlns="">
            <method>removeMembers</method>
            <activityID></activityID>
            <branchID></branchID>
            <members></members>
        </mx:request>
    </mx:HTTPService>
    
    <mx:Script>
		<![CDATA[
			//imports
			import mx.events.CloseEvent;
			import mx.events.DropdownEvent;
			import mx.events.DragEvent;
			import mx.collections.XMLListCollection;
			import mx.core.UITextField;
			import mx.rpc.events.ResultEvent;
			import mx.controls.Spacer;
			import mx.controls.Button;
			import mx.containers.ControlBar;
			import mx.controls.dataGridClasses.DataGridColumn;
			import mx.collections.ArrayCollection;
			import mx.containers.Panel;
			import mx.controls.DataGrid;
			import mx.controls.Alert;
			import mx.managers.PopUpManager;
			import mx.containers.TitleWindow;
			import flash.text.TextField;
			
			// collection of panels
			private var panels:ArrayCollection = new ArrayCollection();
			
			// collection of panels respective dataproviders (also collections)
			private var panelLearnerDataProviders:ArrayCollection = new ArrayCollection();
			
			// initial learners collection (used by global datagrid)
			private var intialLearners:ArrayCollection = new ArrayCollection();
			
			// total number of branches add (even after removal)
			private var totalBranchesAdded:int = 0;
			
			// last drag event stored
			private var lastDragEvent:DragEvent;
			
			// variables grabbed from flashvars
			private var lessonID:int;
			private var activityID:int;
			private var serverUrl:String;
			private var mayDelete:Boolean;
			private var title:String;
			private var languageXML:XML;
			private var viewMode:Boolean;
			
			// constants
			private const SERVLET_PATH:String = "monitoring/chosenBranching.do";
						
			// initializes application
			private function initApp():void {
				// get flashvars
				lessonID = Application.application.parameters.lessonID;
				activityID = Application.application.parameters.activityID;
				serverUrl = Application.application.parameters.serverUrl;
				title = Application.application.parameters.title;
				mayDelete = stringToBool(Application.application.parameters.mayDelete);
				languageXML = XML(Application.application.parameters.languageXML);
				viewMode = stringToBool(Application.application.parameters.viewMode);
				
				// initialize misc. stuff including events
				mainDataGrid_dg.dataProvider = intialLearners;
				mainDataGrid_dg.addEventListener(DragEvent.DRAG_DROP, dragDropHandler);
				finish_btn.addEventListener(MouseEvent.CLICK, closeWindowHandler);	
				
				// if flashvars from jsp are missing
				if(lessonID == 0 || activityID == 0 || serverUrl == "" || languageXML == null) {
					Alert.show("Error occured: flashvars not initialized");
					application.enabled = false;
				}
				else {
					// if may not delete, alert the monitor that learners can be added but not removed from branches
					if(!mayDelete){
						Alert.show(languageXML.language.entry.(@key=="label.branching.popup.drag.selection.message").name);
					}
					initServices();
					initButtonsAndDragging();
					initLabels();
					getMembersNotBranchedService.send();
					getBranchesService.send();
				}
			}
			
			// initialize labels from language xml
			private function initLabels():void{
				finish_btn.label = languageXML.language.entry.(@key=="button.finished").name;
				mainDataGridCol1_col.headerText = languageXML.language.entry.(@key=="label.branching.non.allocated.users.heading").name;
			}
			
			// initialize buttons and dragging depending on jsp conditions
			private function initButtonsAndDragging():void {
				// allow movement of students if deleting is allowed
				mainDataGrid_dg.dragEnabled = true;
				mainDataGrid_dg.dropEnabled = true;
				mainDataGrid_dg.dragMoveEnabled = true;
				mainDataGrid_dg.allowDragSelection = true;
			}
			
			// set request parameters
			private function initServices():void {
				getBranchesService.url = serverUrl + SERVLET_PATH;
				getMembersNotBranchedService.url = serverUrl + SERVLET_PATH;
				getBranchMembersService.url = serverUrl + SERVLET_PATH;
				addMembersService.url = serverUrl + SERVLET_PATH;
				removeMembersService.url = serverUrl + SERVLET_PATH;
				
				getBranchesService.request.activityID = activityID;
				
				getMembersNotBranchedService.request.activityID = activityID;
				getMembersNotBranchedService.request.lessonID = lessonID;
				
				getBranchMembersService.request.activityID = activityID;
				
				addMembersService.request.activityID = activityID;
				
				removeMembersService.request.activityID = activityID;
			}
			
			
			// adds a new branch panel
			private function addBranchPanel(id:String = null, title:String = null):Boolean {
				// create components
                var newPanel:Panel = new Panel();
               	var newDataGrid:DataGrid = new DataGrid();
               	var newLearnersCollection:ArrayCollection = new ArrayCollection([]);
               	var newDataGridColumn:DataGridColumn = new DataGridColumn();
				
				// increment total number of branch panels added
				totalBranchesAdded++;
				
               	// push new branch panel and its dataprovider into their respective collections
               	panels.addItem(newPanel);
               	panelLearnerDataProviders.addItem(newLearnersCollection);
             	             	
             	// set new panel attributes
                newPanel.width = learnerBranches_tile.width / 2 - 15;
                newPanel.height = learnerBranches_tile.height / 2 - 6;
                
                if(id == null){
 					newPanel.id = "branch" + String(totalBranchesAdded) + "Learners_pnl";            
               	}
               	else {
               		newPanel.id = id;
               	}
               	    
                if(title == null){
 					newPanel.title = "Branch " + String(totalBranchesAdded);               
               	}
               	else {
               		newPanel.title = title;
               	}

				// set new column attributes
				newDataGridColumn.headerText = languageXML.language.entry.(@key=="label.grouping.learners").name;
				newDataGridColumn.dataField = "displayName";
				
				// set new datagrid attributes
               	newDataGrid.columns = [newDataGridColumn];
               	newDataGrid.percentWidth = 100;
               	newDataGrid.percentHeight = 100;
               	newDataGrid.allowMultipleSelection = true;
               	newDataGrid.addEventListener(DragEvent.DRAG_DROP, dragDropHandler);
               	newDataGrid.dataProvider = newLearnersCollection;
				
               	newDataGrid.dragEnabled = mayDelete;
               	newDataGrid.dragMoveEnabled= mayDelete;
               	newDataGrid.allowDragSelection = mayDelete;
               	newDataGrid.dropEnabled = true;
               	
				// add elements to the panel
				newPanel.addChild(newDataGrid);
				
				// add the panel to the tile
				learnerBranches_tile.addChild(newPanel);
				
				return true;
			}
					                
	        // find a panel index from its branchID
	        private function findPanelIndex(branchID:int):int{
	        	for(var i:int = 0; i < panels.length; i++) {
	     			if(Panel(panels[i]).id == String(branchID)){
						return i;
	     			}
	     		}
	     		
	     		return -1;
	        }
	        
	        // convert a string to a boolean
	        private function stringToBool(string:String):Boolean{
	        	switch(string){
	        		case "1":
	        		case "true":
	        		case "yes":
	        			return true;
	        		case "0":
	        		case "false":
	        		case "no":
	        			return false;
	        		default:
	        		return Boolean(string);
	        	}
	        }
	                            
	        // click handler for the finish button
	        private function closeWindowHandler(e:MouseEvent):void{
	        	ExternalInterface.call("closeWindow");
	        }
	              
	        // handler to complete dragging users
	        private function dragDropHandler(e:DragEvent):void {
	        	// if the drag and drop source and destination are not the same
	        	if(e.dragInitiator != e.currentTarget){
	        		// get selected items in datagrid
		        	var selectedNames:Array = DataGrid(e.dragInitiator).selectedItems;
		        	var selectedNamesString:String = "";
		        	
		        	// format the learner ids as needed serverside
		        	for(var i:int = 0; i < selectedNames.length; i++){
		        		selectedNamesString += selectedNames[i].id;
		        		
		        		if(i < selectedNames.length - 1)
		        		{
		        			selectedNamesString += ",";
		        		}
		        	}
					
					// if the source is not the initial learners grid, remove members
					if(DataGrid(e.dragInitiator).id != "mainDataGrid_dg") {
						removeMembersService.request.branchID = Panel(e.dragInitiator.parent).id;
						removeMembersService.request.members = selectedNamesString;
						removeMembersService.send();
					}
					
					// if the destination is not the initial learners grid, add members
					if(DataGrid(e.currentTarget).id != "mainDataGrid_dg") {
			        	addMembersService.request.branchID = Panel(e.currentTarget.parent).id;
			        	addMembersService.request.members = selectedNamesString;
			        	addMembersService.send();
					}
	        	}
	        }
	        
	        // -- httpservice handlers --
	        private function getBranchesSuccessHandler(e:ResultEvent):void {
	        	var xmlResult:XMLListCollection = new XMLListCollection(XMLList(e.result).branches.branch);
	        	
	        	for(var i:int = 0; i < xmlResult.length; i++){
					addBranchPanel(xmlResult.getItemAt(i).id.toString(), xmlResult.getItemAt(i).name.toString());
	        	}
	        	
	        	for(var i:int = 0; i < xmlResult.length; i++){
	        		getBranchMembersService.request.branchID = xmlResult.getItemAt(i).id.toString();
	        		getBranchMembersService.send();
	        	}
	        }
	        
	      	private function getBranchesFaultHandler(e:Event):void {
	      		Alert.show(languageXML.language.entry.(@key=="error.title").name);
	        }
	       
	        private function getMembersNotBranchedSuccessHandler(e:ResultEvent):void {
	        	var xmlResult:XMLListCollection = new XMLListCollection(XMLList(e.result).users.user);
	        	var branchID:int = XMLList(e.result).branchID.toString();
	     		
	        	for(var i:int = 0; i < xmlResult.length; i++){
	        		intialLearners.addItem({displayName: xmlResult.getItemAt(i).displayName.toString(), id: xmlResult.getItemAt(i).id.toString()});
	        	}
	        }
	        
	      	private function getMembersNotBranchedFaultHandler(e:Event):void {
	      		Alert.show(languageXML.language.entry.(@key=="error.title").name);
	        }
	        
	        private function getBranchMembersSuccessHandler(e:ResultEvent):void {
	        	var xmlResult:XMLListCollection = new XMLListCollection(XMLList(e.result).users.user);
	        	var branchID:int = XMLList(e.result).branchID.toString();
	     		var indexToUse:int = findPanelIndex(branchID);
	     		
	        	for(var i:int = 0; i < xmlResult.length; i++){
	        		panelLearnerDataProviders[indexToUse].addItem({displayName: xmlResult.getItemAt(i).displayName.toString(), id: xmlResult.getItemAt(i).id.toString()});
	        	}
	        }
	        
	      	private function getBranchMembersFaultHandler(e:Event):void {
	      		Alert.show(languageXML.language.entry.(@key=="error.title").name);
	        }
	        
	        private function addMembersSuccessHandler(e:ResultEvent):void {
	        	// nothing to do here
	        }
	        
	      	private function addMembersFaultHandler(e:Event):void {
	      		Alert.show(languageXML.language.entry.(@key=="error.title").name);
	        }
	        
	        private function removeMembersSuccessHandler(e:ResultEvent):void {
	        	// nothing to do here
	        }
	        
	      	private function removeMembersFaultHandler(e:Event):void {
	      		Alert.show(languageXML.language.entry.(@key=="error.title").name);
	        }
	        
	        private function addBranchSuccessHandler(e:ResultEvent):void {
	        	addBranchPanel(XMLList(e.result).branch.id.toString()), XMLList(e.result).branch.name.toString();
	        }
	        	      	
	      	private function changeBranchNameSuccessHandler(e:ResultEvent):void {
	        	var branchID:int = XMLList(e.result).branch.id.toString();
	        	var branchName:String = XMLList(e.result).branch.newName.toString();
	        	var indexToUse:int = findPanelIndex(branchID);
	        	
	        	Panel(panels.getItemAt(indexToUse)).title = branchName;
	        }
	        
	      	private function changeGroupNameFaultHandler(e:Event):void {
	      		Alert.show(languageXML.language.entry.(@key=="error.title").name);
	        }
	        
	        // -- getters and setters --
	        
	        public function getLanguageXML():XML{
	        	return languageXML;
	        }
		]]>
	</mx:Script>

	<mx:DataGrid id="mainDataGrid_dg" width="185" dragEnabled="true" dropEnabled="true" dragMoveEnabled="true" allowMultipleSelection="true" allowDragSelection="true" left="10" top="10" bottom="10">
		<mx:columns>
			<mx:DataGridColumn id="mainDataGridCol1_col" dataField="displayName"/>
		</mx:columns>
	</mx:DataGrid>
	<mx:Tile verticalScrollPolicy="auto" id="learnerBranches_tile" right="10" top="10" bottom="48" left="203">
	</mx:Tile>
	<mx:HBox verticalAlign="middle" bottom="10" right="10" height="30" left="203">
		<mx:Spacer width="100%"/>
		<mx:Button id="finish_btn"/>
		<mx:SWFLoader visible="false">
			<mx:source>../bin-debug/loading_small.swf</mx:source>
		</mx:SWFLoader>
	</mx:HBox>
	
</mx:Application>
