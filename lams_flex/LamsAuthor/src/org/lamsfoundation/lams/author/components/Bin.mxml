<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" height="65" width="52" 
	dragExit="dragExit(event);" 
	dragDrop="dragDrop(event);"
	dragEnter="dragEnter(event);"
	>

	<mx:Script>
		<![CDATA[
			import org.lamsfoundation.lams.author.events.TransitionEvent;
			import org.lamsfoundation.lams.author.components.transition.TransitionComponent;
			import org.lamsfoundation.lams.author.events.AuthorEvent;
			import org.lamsfoundation.lams.author.components.activity.ActivityComponent;
			import mx.managers.DragManager;
			import org.lamsfoundation.lams.author.util.Constants;
			import org.lamsfoundation.lams.author.events.AuthorActivityEvent;
			import mx.core.Application;
			import mx.events.DragEvent;

			
			[Bindable]
			[Embed("assets/icons/bin.png")] 
			private var bin:Class;
			
			[Bindable]
			[Embed("assets/icons/bin_over.png")] 
			private var binOver:Class;
			
			private function dragEnter(event:DragEvent):void {
				if (event.dragSource.hasFormat("img"))
                {
                    DragManager.acceptDragDrop(Canvas(event.currentTarget));
                    image.source = binOver;
                }
			}
			
			private function dragExit(event:DragEvent):void {
				image.source = bin;
			}
			
			private function dragDrop(event:DragEvent):void {
				image.source = bin;
				
				if (event.dragInitiator is ActivityComponent) {
            		var activityComponent:ActivityComponent = event.dragInitiator as ActivityComponent;
					var activityEvent:AuthorActivityEvent = new AuthorActivityEvent(AuthorActivityEvent.DELETE_ACTIVITY_EVENT);
					activityEvent.activityComponent = activityComponent;
					Application.application.dispatchEvent(activityEvent);
				} else if (event.dragInitiator is TransitionComponent) {
					var transitionComponent:TransitionComponent = event.dragInitiator as TransitionComponent;
					var transitionEvent:TransitionEvent = new TransitionEvent(TransitionEvent.DELETE_TRANSITION);
					transitionEvent.transition = transitionComponent;
					Application.application.dispatchEvent(transitionEvent);
				}
			}
		]]>
	</mx:Script>
	


	<mx:Image id="image" source="{bin}" styleName="binImage" height="65" width="52"/>

	
</mx:Canvas>
